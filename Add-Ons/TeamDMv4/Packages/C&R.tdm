//Cops and Robbers
//3
//Cops: Arrest all the Robbers by getting them to half HP (Red names) and using the Police Baton!
//Robbers: Get $100 to your Base by raiding the Bank and stealing from it.
//Place a Bank Trigger, Jail Spawn and Robber Base brick to start the game.
//cr
//0

function crCtrlSO::tdmStart()
{
 if((%a = $TeamDM::packagemanager.tdmcheck()) !$= "1")
 {
  servercmdStartPackage(-1,"Cops and Robbers");
  messageall('',"(" @ %a @ ")");
  return;
 }
 $TeamDM::CR::Money = 0;
 $TeamDM::CR::CopDeaths = 0;
 $TeamDM::CR::CopWins = 0;
 $TeamDM::CR::RobWins = 0;
 $TeamDM::CR::GameWon = 0;
 for(%i=0;%i<mainbrickgroup.getCount();%i++)
 {
  for(%j=0;%j<mainbrickgroup.getObject(%i).getCount();%j++)
  {
   %obj = mainbrickgroup.getObject(%i).getObject(%j).getdatablock().getName();
   if(%obj $= "brickCRJailData")
   {
    if(isObject($TeamDM::CR::JailSpawn)){return;}
    $TeamDM::CR::JailSpawn = mainbrickgroup.getObject(%i).getObject(%j);
   }
  }
 }
 messageall('',"\c5The \c7Robbers\c5 have to get $" @ $server::playercount*25 @ " to win this round!");
 $TeamDM::CR::WinAmount = $server::playercount*25;
		 for(%i=0;%i<clientgroup.getcount();%i++)
		 {
			clientgroup.getobject(%c).crscore = 0;
		 }
}
function crCtrlSO::tdmCheck()
{
 for(%i=0;%i<mainbrickgroup.getCount();%i++)
 {
  for(%j=0;%j<mainbrickgroup.getObject(%i).getCount();%j++)
  {
   %obj = mainbrickgroup.getObject(%i).getObject(%j).getdatablock().getName();
   if(%obj $= "brickCRBankData"){%bank = 1;}
   if(%obj $= "brickCRJailData"){%jail = 1;}
   if(%obj $= "brickCRRobBaseData"){%base = 1;}
  }
 }
 if(!%bank){return "\c5A \c0Bank Trigger\c5 does not exist.";}
 if(!%jail){return "\c5A \c0Jail Spawn\c5 does not exist.";}
 if(!%base){return "\c5A \c0Robber Base Brick\c5 does not exist.";}
 return 1;
}
function crCtrlSO::tdmSettings(%ctrl,%client)
{
 clearTeams();
 for(%j=0;%j<ClientGroup.getCount();%j++)
 {
  %cl = ClientGroup.getObject(%j);
  if(%cl.tdmTeam $= %team && $TeamDM::TeamsOn && isObject(%cl.minigame))
  {
   schedule(100,0,respawn,%cl);
  }
 }
 loadTDMSave(%client,"Add-Ons/TeamDMv4/Saves/C&R_DefaultUniforms.tsave");
 //if(!isTeam("Cops")){newTeam("Cops","blue");}
 //if(!isTeam("Robbers")){newTeam("Robbers","grey");}
 //$TeamDM::PackageTeamLock = 1;
 //schedule(200,0,eval,"$TeamDM::PackageTeamLock = 1;");
}
function crCtrlSO::tdmEnd()
{
 $TeamDM::CR::Money = 0;
 $TeamDM::CR::WinAmount = 0;
   $TeamDM::CR::CopDeaths = 0;
 $TeamDM::CR::CopWins = 0;
 $TeamDM::CR::RobWins = 0;
}
function crCtrlSO::respawn(%obj,%this)
{
 %this.player.isJailed = 0;
 %this.player.crmoney = 0;
}
function crCtrlSO::OnDeath(%obj,%client, %sourceObject, %sourceClient, %damageType, %damLoc)
{
 if(%client.tdmteam.name $= "Robbers" && %sourceclient.tdmTeam.name $= "Cops")
 {
  messageclient(%sourceclient,'',"\c5Hit the \c7Robbers \c5with \c0Red Names\c5 to Jail them and win.");
  messageclient(%sourceclient,'',"\c5If you kill them, they respawn with full health and can't be jailed!");
 }
 if(%client.tdmteam.name $= "Robbers")
 {
  %client.schedule(1000,createPlayer);
  schedule(1100,0,respawnfix,%client);
 }
 if(%client.tdmteam.name $= "Cops" && %sourceClient.tdmTeam.name $= "Robbers")
 {
  $TeamDM::CR::CopDeaths++;
  if($TeamDM::CR::CopDeaths >= 3)
  {
  %lowest = 20000000;
  %found = -1;
  for(%i=0;%i<clientgroup.getcount();%i++)
  {
   %cl = clientgroup.getobject(%i);
   if(%cl.tdmteam.name !$= "Robbers" || !%cl.player.isJailed || !isObject(%cl.player))
    continue;
   if(%cl.player.jailtime > %lowest)
    continue;
   %lowest = %cl.player.jailtime;
   %found = %cl;
  }
   if(!isObject(%found)){return;}
   %cl = %found;
   %cl.player.isJailed = 0;
   $TeamDM::CR::CopDeaths = 0;
   schedule(100,0,respawn,%cl);
   messageall('',"<color:" @ $TeamDM::Color[%sourceclient.tdmteam.color,1] @ ">" @ %sourceclient.name @ "\c5 freed <color:" @ $TeamDM::Color[%cl.tdmteam.color,1] @ ">" @ %cl.name @ "\c5 from jail!");
   return;
 }
 }
}
function respawnfix(%client){commandtoclient(%client,'centerprint',"",0,0);}
function crCtrlSO::onClientLeaveGame(%obj,%client)
{
 //bye
}

package crfunctions
{
 function uniform(%player,%team)
 {
 if(!isObject(%player) || !isObject(%team)){return;}
 %lead = (%team.leader $= %player.client || (%team.name $= "Robbers" && %player.isjailed && $TeamDM::PackageOn $= "Cops and Robbers" && $TeamDM::TeamsOn));
 hideAllNodes(%player);
 if(%lead) //Ugh, %a["String"]b doesn't work?
 {
  if(%player.getDatablock().shapefile !$= "base/data/shapes/player/m.dts"){%player.setNodeColor("body",%team.setLEADchestcolor);return;}
  if($accent[%team.setLEADaccent] !$= "none")
  {
   if($hat[%team.setLEADhat] !$= "helmet")
   {
     %player.unhidenode($accent[%team.setLEADaccent]);
     %player.setnodecolor($accent[%team.setLEADaccent],%team.setLEADaccentcolor);
   }
   else
   {
     %player.unhidenode(visor);
     %player.setnodecolor(visor,%team.setLEADaccentcolor);
   }
  }
 %player.unhidenode($chest[%team.setLEADchest]);
  %player.setnodecolor($chest[%team.setLEADchest],%team.setLEADchestcolor);
 if($hat[%team.setLEADhat] !$= "none")
 {
 %player.unhidenode($hat[%team.setLEADhat]);
  %player.setnodecolor($hat[%team.setLEADhat],%team.setLEADhatcolor);
 }
 %player.unhidenode($lleg[%team.setLEADlleg]);
  %player.setnodecolor($lleg[%team.setLEADlleg],%team.setLEADllegcolor);
 %player.unhidenode($rleg[%team.setLEADrleg]);
  %player.setnodecolor($rleg[%team.setLEADrleg],%team.setLEADrlegcolor);
 %player.unhidenode($lhand[%team.setLEADlhand]);
  %player.setnodecolor($lhand[%team.setLEADlhand],%team.setLEADlhandcolor);
 %player.unhidenode($rhand[%team.setLEADrhand]);
  %player.setnodecolor($rhand[%team.setLEADrhand],%team.setLEADrhandcolor);
 %player.unhidenode($larm[%team.setLEADlarm]);
  %player.setnodecolor($larm[%team.setLEADlarm],%team.setLEADlarmcolor);
 %player.unhidenode($rarm[%team.setLEADrarm]);
  %player.setnodecolor($rarm[%team.setLEADrarm],%team.setLEADrarmcolor);
 if($pack[%team.setLEADpack] !$= "none"){
 %player.unhidenode($pack[%team.setLEADpack]);
  %player.setnodecolor($pack[%team.setLEADpack],%team.setLEADpackcolor);}
 if($secondpack[%team.setLEADsecondpack] !$= "none"){
 %player.unhidenode($secondpack[%team.setLEADsecondpack]);
  %player.setnodecolor($secondpack[%team.setLEADsecondpack],%team.setLEADsecondpackcolor);}
 if(%team.setLEADhip == 0)
 {
  %player.unhidenode("pants");%player.setnodecolor("pants",%team.setLEADhipcolor);
 }
 else
 {
  %player.unhidenode("skirthip");%player.setnodecolor("skirthip",%team.setLEADhipcolor);
  %player.unhideNode("skirtTrimLeft");%player.setnodecolor("skirttrimleft",%team.setLEADllegcolor);
  %player.unhideNode("skirtTrimRight");%player.setnodecolor("skirttrimright",%team.setLEADrlegcolor);
 }
 %player.unhidenode(headSkin);
  %player.setnodecolor(headSkin,%team.setLEADheadcolor);
 %player.setFaceName(%team.setLEADfacename);
 %player.setDecalName(%team.setLEADdecalname);
 }
 else
 {
  if(%player.getDatablock().shapefile !$= "base/data/shapes/player/m.dts"){%player.setNodeColor("body",%team.setchestcolor);return;}
  if($accent[%team.setaccent] !$= "none")
  {
   if($hat[%team.sethat] !$= "helmet")
   {
     %player.unhidenode($accent[%team.setaccent]);
     %player.setnodecolor($accent[%team.setaccent],%team.setaccentcolor);
   }
   else
   {
     %player.unhidenode(visor);
     %player.setnodecolor(visor,%team.setaccentcolor);
   }
  }
 %player.unhidenode($chest[%team.setchest]);
  %player.setnodecolor($chest[%team.setchest],%team.setchestcolor);
 if($hat[%team.sethat] !$= "none")
 {
  %player.unhidenode($hat[%team.sethat]);
  %player.setnodecolor($hat[%team.sethat],%team.sethatcolor);
 }
 %player.unhidenode($lleg[%team.setlleg]);
  %player.setnodecolor($lleg[%team.setlleg],%team.setllegcolor);
 %player.unhidenode($rleg[%team.setrleg]);
  %player.setnodecolor($rleg[%team.setrleg],%team.setrlegcolor);
 %player.unhidenode($lhand[%team.setlhand]);
  %player.setnodecolor($lhand[%team.setlhand],%team.setlhandcolor);
 %player.unhidenode($rhand[%team.setrhand]);
  %player.setnodecolor($rhand[%team.setrhand],%team.setrhandcolor);
 %player.unhidenode($larm[%team.setlarm]);
  %player.setnodecolor($larm[%team.setlarm],%team.setlarmcolor);
 %player.unhidenode($rarm[%team.setrarm]);
  %player.setnodecolor($rarm[%team.setrarm],%team.setrarmcolor);
 if($pack[%team.setpack] !$= "none"){
 %player.unhidenode($pack[%team.setpack]);
  %player.setnodecolor($pack[%team.setpack],%team.setpackcolor);}
 if($secondpack[%team.setsecondpack] !$= "none"){
 %player.unhidenode($secondpack[%team.setsecondpack]);
  %player.setnodecolor($secondpack[%team.setsecondpack],%team.setsecondpackcolor);}
 if(%team.sethip == 0)
 {
  %player.unhidenode("pants");%player.setnodecolor("pants",%team.sethipcolor);
 }
 else
 {
  %player.unhidenode("skirthip");%player.setnodecolor("skirthip",%team.sethipcolor);
  %player.unhideNode("skirtTrimLeft");%player.setnodecolor("skirttrimleft",%team.setllegcolor);
  %player.unhideNode("skirtTrimRight");%player.setnodecolor("skirttrimright",%team.setrlegcolor);
 }
 %player.unhidenode(headSkin);
  %player.setnodecolor(headSkin,%team.setheadcolor);
 %player.setFaceName(%team.setfacename);
 %player.setDecalName(%team.setdecalname);
 }
 }
 function servercmdsuicide(%client)
 {
  if(%client.tdmteam.name $= "Robbers" && %client.player.isJailed)
  {
   return;
  }
  Parent::servercmdsuicide(%client);
 }
function Armor::damage(%this, %obj, %sourceObject, %position, %damage, %damageType)
{
   if (%obj.getState() $= "Dead")
      return;
  if(%obj.isJailed)
      return;
  Parent::damage(%this, %obj, %sourceObject, %position, %damage, %damageType);
  if(%obj.getDamagePercent() > 0.5 && %obj.client.tdmteam.name $= "Robbers" && $TeamDM::PackageOn $= "Cops and Robbers" && $TeamDM::TeamsOn){%obj.setShapeNameColor("1 0 0 1");}
 }
 function batProjectile::damage(%this,%obj,%col,%fade,%pos,%normal)
 {
  if($TeamDM::PackageOn !$= "Cops and Robbers"){return;}
  if(!isObject(%obj.client) || !isObject(%col.client)){return;}
  if(!$TeamDM::TeamsOn || %obj.client.tdmTeam.name !$= "Cops")
  {
   %col.setVelocity("0 0 15");
  }
  if(!isObject(%obj.client.player) || %obj.client.player.getState() $= "Dead" || %col.getState() $= "Dead" || %col.getClassName() !$= "Player" || !$TeamDM::TeamsOn || %obj.client.tdmTeam.name !$= "Cops" || %col.client.tdmTeam.name !$= "Robbers" || %col.getDamagePercent() < 0.5){return;}
  %col.isjailed = !%col.isjailed; 
  if(%col.isJailed)
  {
   messageall('',"<color:" @ $TeamDM::Color[%obj.client.tdmteam.color,1] @ ">" @ %obj.client.name @ "\c5 captured <color:" @ $TeamDM::Color[%col.client.tdmteam.color,1] @ ">" @ %col.client.name @ "\c5!");
   if(isObject($TeamDM::CR::JailSpawn))
   {
    %col.unmountimage(0);
    %col.setShapeNameColor($TeamDM::Color[%col.client.team.color,0] SPC "1");
    $TeamDM::CR::CopDeaths = 0;
    %col.jailtime = $Sim::Time;
    %col.setTransform($TeamDM::CR::JailSpawn.getTransform());%col.setVelocity(0);
    uniform(%col,%col.client.tdmteam);
   }
   for(%i=0;%i<clientgroup.getcount();%i++)
   {
    if(clientgroup.getObject(%i).tdmteam.name $= "Robbers" && !clientgroup.getObject(%i).player.isJailed && isObject(clientgroup.getobject(%i).player)){return;}
   }
   if($TeamDM::CR::GameWon){return;}
		 $TeamDM::CR::GameWon = 1;
		 $TeamDM::CR::CopWins++;
		 messageall('',"\c5The <color:" @ $TeamDM::Color[%obj.client.tdmteam.color,1] @ ">Cops\c5 have won! (Captured all Robbers!)");
		 messageall('',"\c5Restarting in 5 seconds.");
		 schedule(5000,0,crrestart);
		 for(%i=0;%i<clientgroup.getcount();%i++)
		 {
			%c = clientgroup.getobject(%i);
			if(%c.tdmteam.name $= "Cops")
			{
				%c.crscore++;
			}
		}
  }
  else
  {
   messageall('',"<color:" @ $TeamDM::Color[%obj.client.tdmteam.color,1] @ ">" @ %obj.client.name @ "\c5 freed <color:" @ $TeamDM::Color[%col.client.tdmteam.color,1] @ ">" @ %col.client.name @ "\c5 from jail!");
   respawn(%col.client);
  }
 }
 function servercmdusetool(%client,%slot)
 {
  if(%client.player.isJailed){return;}
  Parent::servercmdusetool(%client,%slot);
 }
 function crrestart()
 {
		 $teamDM::CR::GameWon = 0;
		 messageall('',"\c5Everyone swaps sides...");
		 $teamDM::cr::money = 0;
		 messageall('',"\c5The \c7Robbers\c5 have to get $" @ $server::playercount*25 @ " to win this round!");
		 $TeamDM::CR::WinAmount = $server::playercount*25;
		 for(%i=0;%i<clientgroup.getcount();%i++)
		 {
			%c = clientgroup.getobject(%i);
			%c.player.isJailed = 0;
			if(%c.tdmteam $= $teamdm::teammanager.team[0])
			{
			 servercmdSetTeamGUI(-1,2,%i);
			}
			else
			{
			 servercmdSetTeamGUI(-1,1,%i);
			}
		 }
 }
function tdmShowScores()
{
 if($TeamDM::PackageOn !$= "Cops and Robbers"){Parent::tdmShowScores();return;}
 $TeamDM::Score = schedule(60000,0,tdmShowScores);
		 for(%i=0;%i<clientgroup.getcount();%i++)
		 {
			%c = clientgroup.getobject(%i);
			if(isObject(%c.tdmTeam))
			{
			%str = "<color:" @ $TeamDM::Color[%c.tdmteam.color,1] @ ">" @ %c.name @ "\c5 has won in " @ %c.crscore @ (%c.crscore !$= "1" ? (%c.crscore $= "" ? "0 rounds." : " rounds.") : " round.");
			}
			else
			{
			%str = "\c3" @ %c.name @ "\c5 has won in " @ %c.crscore @ (%c.crscore !$= "1" ? (%c.crscore $= "" ? "0 rounds." : " rounds.") : " round.");
			}
			schedule(2000 * %i,0,commandtoall,'bottomprint',%str,2,2,1);
		 }
}
function CRBankTrigger::onEnterTrigger(%this,%trigger,%obj)
{
	if(isObject(%obj.client) && isObject(%obj.client.minigame) && $TeamDM::TeamsOn && %obj.client.tdmteam.name $= "Robbers" && !%obj.isjailed && %obj.crmoney < 1 && $TeamDM::PackageOn $= "Cops and Robbers")
	{
		messageallexcept(%obj.client,-1,'',"<color:" @ $TeamDM::Color[%obj.client.tdmteam.color,1] @ ">" @ %obj.client.name @ "\c5 is stealing from the \c3Bank\c5!");
		messageclient(%obj.client,'',"\c5You stole $50 from the \c3Bank!");
		%obj.crmoney = 50;
	}
}
function CRBaseTrigger::onEnterTrigger(%this,%trigger,%obj)
{
	if(!$TeamDM::CR::GameWon && isObject(%obj.client) && isObject(%obj.client.minigame) && $TeamDM::TeamsOn && %obj.client.tdmteam.name $= "Robbers" && %obj.crmoney >= 50 && $TeamDM::PackageOn $= "Cops and Robbers")
	{
		messageall('',"<color:" @ $TeamDM::Color[%obj.client.tdmteam.color,1] @ ">" @ %obj.client.name @ "\c5 got back to base with $50.");
		$TeamDM::CR::Money+=50;
		if($TeamDM::CR::Money < $TeamDM::CR::WinAmount)
		{
		 messageall('',"\c5The <color:" @ $TeamDM::Color[%obj.client.tdmteam.color,1] @ ">Robbers\c5 now have $" @ $TeamDM::CR::Money @ ". ($" @ $teamDM::cr::winamount - $Teamdm::cr::money @ " to go...)");
		 %obj.crmoney = 0;
		}
		else
		{
		 $TeamDM::CR::GameWon = 1;
		 $TeamDM::CR::RobWins++;
		 messageall('',"\c5The <color:" @ $TeamDM::Color[%obj.client.tdmteam.color,1] @ ">Robbers\c5 have won! ($" @ $TeamDM::CR::Money @ "/$" @ $teamDM::cr::winamount @ ")");
		 messageall('',"\c5Restarting in 5 seconds.");
		 schedule(5000,0,crrestart);
		 for(%i=0;%i<clientgroup.getcount();%i++)
		 {
			%c = clientgroup.getobject(%i);
			if(%c.tdmteam.name $= "Robbers")
			{
				%c.crscore++;
			}
		}
		}
	}
}
};activatepackage(crfunctions);

////----Datablocks and Code----\\\\
datablock fxDTSBrickData(brickCRBankData : brick2x2data)
{
 uiName = "(C&R) Bank Trigger";
 Category = "Special";
 subCategory = "Team DM";
};

datablock fxDTSBrickData(brickCRRobBaseData : brick2x2data)
{
 uiName = "(C&R) Robber Base";
 Category = "Special";
 subCategory = "Team DM";
};

datablock fxDTSBrickData(brickCRJailData : brickSpawnPointData)
{
 uiName = "(C&R) Jail Spawn";
 Category = "Special";
 subCategory = "Team DM";
};

datablock TriggerData(CRBankTrigger)
{
 tickPeriodMS = 0;
};

function CRBankTrigger::onTickTrigger(%this,%trigger)
{
}

function CRBankTrigger::onLeaveTrigger(%this,%trigger,%obj)
{
}

function brickCRBankData::onPlant(%this,%brick)
{
 Parent::onPlant(%this,%brick);
 schedule(100,0,TriggerMakeBrick,%brick,CRBankTrigger);
}
function brickCRBankData::onRemove(%this,%brick)
{
 Parent::onRemove(%this,%brick);
 if(isObject(%brick.trigger)){%brick.trigger.delete();}
}

datablock TriggerData(CRBaseTrigger)
{
 tickPeriodMS = 0;
};

function CRBaseTrigger::onTickTrigger(%this,%trigger)
{
}

function CRBaseTrigger::onLeaveTrigger(%this,%trigger,%obj)
{
}

function brickCRRobBaseData::onPlant(%this,%brick)
{
 Parent::onPlant(%this,%brick);
 schedule(100,0,TriggerMakeBrick,%brick,CRBaseTrigger);
}
function brickCRRobBaseData::onRemove(%this,%brick)
{
 Parent::onRemove(%this,%brick);
 if(isObject(%brick.trigger)){%brick.trigger.delete();}
}

function TriggerMakeBrick(%brick,%trig)
{
	%dist1 = getWords(%brick.getWorldBox(),0,2);
	%dist2 = getWords(%brick.getWorldBox(),3,6);
	%x = mAbs(getWord(%dist1,0) - getWord(%dist2,0));
	%y = mAbs(getWord(%dist1,1) - getWord(%dist2,1));
	%z = mAbs(getWord(%dist1,2) - getWord(%dist2,2));
	%pos = %brick.position;
	%pos = getWord(%pos,0) SPC getWord(%pos,1) SPC getWord(%pos,2);
	%p = new Trigger()
	{
		datablock = %trig;
		position = vectorAdd(%pos,-%x/2-0.1 SPC %y/2+0.1 SPC -%z/2-0.1);
		polyhedron = "0.0000000 0.0000000 0.0000000 1.0000000 0.0000000 0.0000000 0.0000000 -1.0000000 0.0000000 0.0000000 0.0000000 1.0000000";
		scale = %x+0.2 SPC %y+0.2 SPC %z + 0.5;
		rotation = "1 0 0 0";
	};
	%brick.trigger = %p;
	%p.spawnbrick = %brick;
	%p.player = %brick.getGroup().client.player;
	MissionCleanup.add(%p);
}

datablock ProjectileData(batProjectile : hammerProjectile)
{
   directDamage        = 0;
};

//////////
// item //
//////////
datablock ItemData(batItem : hammerItem)
{
	category = "Weapon";  // Mission editor category
	className = "Weapon"; // For inventory system

	colorShiftColor = "0.0 0.0 0.5 1.000";
	uiname = "Police Baton";
	image = batImage;
	canDrop = false;
};

datablock ShapeBaseImageData(batImage : hammerImage)
{
   item = batItem;
   ammo = " ";
   colorShiftColor = "0.0 0.0 0.5 1.000";
   projectile = batProjectile;
   projectileType = Projectile;
};

function batImage::onPreFire(%this, %obj, %slot)
{
	//messageAll( 'MsgClient', 'sword prefired!!!');
	//Parent::onFire(%this, %obj, %slot);
	%obj.playthread(2, armattack);
}

function batImage::onStopFire(%this, %obj, %slot)
{	
	%obj.playthread(2, root);
	//messageAll( 'MsgClient', 'stopfire');
}
